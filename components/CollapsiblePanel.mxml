<?xml version="1.0" encoding="utf-8"?>
<s:SkinnableContainer 
	xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:s="library://ns.apache.org/royale/spark"
	skinClass="components.CollapsiblePanelSkin"
	width="100%"
	>
	<s:layout>
		<s:VerticalLayout/>
	</s:layout>
	<fx:Metadata>
		[Event(name="closed")]
		[Event(name="opened")]
	</fx:Metadata>
	<fx:Script>
		<![CDATA[

			import spark.components.Button;

			[SkinPart( required="true" )]
			public var header : Button;

			protected var _open : Boolean = true;

			public function set open( open : Boolean ) : void
			{
				_open = open;

				if( header )
				{
					//header.open = _open;
				}
				invalidateSkinState();
			}

			public function get open() : Boolean
			{
				return _open;
			}

			override protected function getCurrentSkinState() : String
			{
				var state : String = open ? "open" : "closed";
				return state;
			}

			override protected function partAdded( partName : String, instance : Object ) : void
			{
				super.partAdded( partName, instance );

				if( instance == header )
				{
					header.addEventListener( MouseEvent.CLICK, onClickHeader );
				}
			}

			override protected function partRemoved( partName : String, instance : Object ) : void
			{
				super.partRemoved( partName, instance );

				if( instance == header )
				{
					header.removeEventListener( MouseEvent.CLICK, onClickHeader );
				}
			}

			protected function onClickHeader( event : MouseEvent ) : void
			{
				open = !open;
			}

		]]>
	</fx:Script>
</s:SkinnableContainer>
